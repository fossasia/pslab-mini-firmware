# System layer - target build configuration
add_library(pslab-system STATIC)

add_subdirectory(bus)
add_subdirectory(instrument)
add_subdirectory(timer)

target_sources(pslab-system
    PRIVATE
        led.c
        system.c
    # Needed by newlib when linking application
    PUBLIC
        stubs.c
        syscalls.c
)

target_include_directories(pslab-system
    INTERFACE
        # Include paths for pslab-system start with "system/..."
        ${CMAKE_CURRENT_SOURCE_DIR}/..
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}
)

target_link_libraries(pslab-system
    PRIVATE
        pslab-util
        pslab-platform
)

# Propagate TinyUSB desciptor callbacks to application
target_link_options(pslab-system
    INTERFACE
        $<TARGET_PROPERTY:pslab-platform,INTERFACE_LINK_OPTIONS>
)
